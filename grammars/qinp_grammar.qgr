\\ ---------------- GLOBAL CODE ----------------

GlobalCode:
    (
        ( NonCodeBlock "\n" )?
        GlobalCodeItem
        (
            NonCodeBlock?
            "\n"_
            GlobalCodeItem
        )*
    )

GlobalCodeItem(hidden):
    StatementImport
    StatementDefer
    SpaceBodyItem

\\ ---------------- PASS ----------------

StatementPass:
    "pass"_

\\ ---------------- DEFER ----------------

StatementDefer:
    "defer"_

\\ ---------------- IMPORT ----------------

StatementImport:
    (
        "import"_
        ImportSpecifiers
        Whitespace_
        String{ onFail: fail("Expected import path") }
    )

ImportSpecifiers:
    (
        "."_
        ImportSpecifier{ onFail: fail("Expected import specifier") }
    )*

ImportSpecifier(hidden):
    "linux"
    "windows"
    "macos"
    "defer"

\\ ---------------- SPACE ----------------

StatementSpace:
    SpaceHeader SpaceBody

SpaceHeader:
    (
        "space"_
        Whitespace_
        SpaceName{ onFail: fail("Expected space name") }
        Whitespace?_
        ":"_{ onFail: fail("Expected colon") }
    )

SpaceName:
    Identifier

SpaceBody:
    Whitespace?_ SpaceBodyItem
    (
        NonCodeBlock?
        "\n"_ IndentationIncrease_{ onFail: fail("Expected indentation increase") }
        SpaceBodyItem{ onFail: fail("Expected at least one space body item") }
        (
            NonCodeBlock?
            "\n"_ Indentation_
            SpaceBodyItem{ onFail: fail("Expected space body item after correct indentation")}
        )*
        IndentationDecrease_
    )

SpaceBodyItem(hidden):
    StatementPass
    StatementSpace
    StatementReturn
    StatementFunctionDeclDef
    StatementVariableDeclDef
    Expression

\\ ---------------- RETURN ----------------

StatementReturn:
    "return"_ Whitespace?_ Expression

\\ ---------------- FUNCTIONS ----------------

StatementFunctionDeclDef:
    FunctionHeader [ FunctionDeclaration FunctionDefinition ]

FunctionHeader:
    (
        "fn"_ Whitespace?_
        FunctionReturnType
        Whitespace?_
        FunctionName{ onFail: fail("Expected function name") }
        FunctionParameters
        Whitespace?_
        FunctionSpecifiers
    )

FunctionName:
    Identifier

FunctionReturnType:
    (
        "<"_ Whitespace?_
        Datatype?
        Whitespace?_ ">"_{ onFail: fail("Expected closing angle bracket") }
    )
    ""_

FunctionParameters:
    (
        "("_ Whitespace?_
        (
            FunctionParameter
            (
                Whitespace?_ ","_
                Whitespace?_
                FunctionParameter{ onFail: fail("Expected function parameter after comma") }
            )*
        )?
        Whitespace?_ ")"_{ onFail: fail("Expected closing parenthesis") }
    )

FunctionParameter:
    ( Datatype Whitespace?_ Identifier{ onFail: fail("Expected function parameter name") } )->Normal

FunctionSpecifiers:
    ( FunctionSpecifier ( Whitespace?_ FunctionSpecifier )* )?

FunctionSpecifier:
    "!"_->RequiresPredeclaration
    "nodiscard"_->NoDiscard

FunctionDeclaration:
    Whitespace?_ "..."_

FunctionDefinition:
    Whitespace?_ ":"_ FunctionBody{ onFail: fail("Expected function body")}

FunctionBody:
    Whitespace?_ FunctionBodyItem
    (
        NonCodeBlock?
        "\n"_ IndentationIncrease_{ onFail: fail("Expected indentation increase") }
        FunctionBodyItem{ onFail: fail("Expected at least one function body item") }
        (
            NonCodeBlock?
            "\n"_ Indentation_
            FunctionBodyItem{ onFail: fail("Expected function body item after correct indentation")}
        )*
        IndentationDecrease_
    )

FunctionBodyItem(hidden):
    SpaceBodyItem

\\ ---------------- VARIABLES ----------------

StatementVariableDeclDef:
    VariableDeclarator VariableDatatype Whitespace?_ VariableName{ onFail: fail("Expected variable name") } VariableInitializer

VariableDeclarator:
    [ "var" "const" ]

VariableDatatype:
    (
        "<"_ Whitespace?_
        Datatype?
        Whitespace?_ ">"_{ onFail: fail("Expected closing angle bracket") }
    )
    ""_

VariableName:
    Identifier

VariableInitializer:
    (
        Whitespace?_
        "="_
        Whitespace?_
        Expression{ onFail: fail("Expected variable initializer value") }
    )?

\\ ---------------- DATATYPES ----------------

Datatype:
    (
        "?"_
        Identifier{ onFail: fail("Expected function blueprint parameter name") }
    )->Blueprint
    (
        Identifier
        Whitespace?_
        ("const"_->Const)?
        (
            Whitespace?_
            "*"_->Pointer
            Whitespace?_
        ("const"_->Const)?
        )*
    )->Named

\\ ---------------- EXPRESSIONS ----------------

Expression:
    ExprPrec1

ExprPrec1:
    (
        ExprPrec2
        (
            Whitespace?_
            ExprPrec1Operator
            Whitespace?_
            ExprPrec2{ onFail: fail("Expected expression after operator [lvl-01]") }
        )*
    )

ExprPrec1Operator:
    [ "=" "+=" "-=" "*=" "/=" "%=" "&=" "|=" "^=" "<<=" ">>=" ]

ExprPrec2:
    (
        ExprPrec3
        (
            Whitespace?_
            ExprPrec2Operator
            Whitespace?_
            ExprPrec3{ onFail: fail("Expected expression after operator [lvl-02]") }
        )*
    )

ExprPrec2Operator:
    [ "||" ]

ExprPrec3:
    (
        ExprPrec4
        (
            Whitespace?_
            ExprPrec3Operator
            Whitespace?_
            ExprPrec4{ onFail: fail("Expected expression after operator [lvl-03]") }
        )*
    )

ExprPrec3Operator:
    [ "&&" ]

ExprPrec4:
    (
        ExprPrec5
        (
            Whitespace?_
            ExprPrec4Operator
            Whitespace?_
            ExprPrec5{ onFail: fail("Expected expression after operator [lvl-04]") }
        )*
    )

ExprPrec4Operator:
    [ "|" ]

ExprPrec5:
    (
        ExprPrec6
        (
            Whitespace?_
            ExprPrec5Operator
            Whitespace?_
            ExprPrec6{ onFail: fail("Expected expression after operato [lvl-05]r") }
        )*
    )

ExprPrec5Operator:
    [ "^" ]

ExprPrec6:
    (
        ExprPrec7
        (
            Whitespace?_
            ExprPrec6Operator
            Whitespace?_
            ExprPrec7{ onFail: fail("Expected expression after operator [lvl-06]") }
        )*
    )

ExprPrec6Operator:
    [ "&" ]

ExprPrec7:
    (
        ExprPrec8
        (
            Whitespace?_
            ExprPrec7Operator
            Whitespace?_
            ExprPrec8{ onFail: fail("Expected expression after operator [lvl-07]") }
        )*
    )

ExprPrec7Operator:
    [ "==" "!=" ]

ExprPrec8:
    (
        ExprPrec9
        (
            Whitespace?_
            ExprPrec8Operator
            Whitespace?_
            ExprPrec9{ onFail: fail("Expected expression after operator [lvl-08]") }
        )*
    )

ExprPrec8Operator:
    [ "<=" "<" ">=" ">" ]

ExprPrec9:
    (
        ExprPrec10
        (
            Whitespace?_
            ExprPrec9Operator
            Whitespace?_
            ExprPrec10{ onFail: fail("Expected expression after operator [lvl-09]") }
        )*
    )

ExprPrec9Operator:
    [ "<<" ">>" ]

ExprPrec10:
    (
        ExprPrec11
        (
            Whitespace?_
            ExprPrec10Operator
            Whitespace?_
            ExprPrec11{ onFail: fail("Expected expression after operator [lvl-10]") }
        )*
    )

ExprPrec10Operator:
    [ "+" "-" ]

ExprPrec11:
    (
        ExprPrec12
        (
            Whitespace?_
            ExprPrec11Operator
            Whitespace?_
            ExprPrec12{ onFail: fail("Expected expression after operator [lvl-11]") }
        )*
    )

ExprPrec11Operator:
    [ "*" "/" "%" ]

ExprPrec12:
    (
        (
            Whitespace?_
            ExprPrec12Operator
        )*
        ExprPrec13
    )

ExprPrec12Operator:
    [ "++" "--" "+" "-" "!" "~" "*" "&" ExprOpTypeCast ]

ExprOpTypeCast:
    (
        "("_ Whitespace?_
        Datatype
        Whitespace?_ ")"_
    )

ExprPrec13:
    (
        ExprPrec14
        (
            Whitespace?_
            ExprPrec13Operator
        )?
    )

ExprPrec13Operator:
    [ "++" "--" "." "->" ExprOpFunctionCall ExprOpSubscript]

ExprOpFunctionCall:
    (
        "("_ Whitespace?_
        (
            Expression
            (
                Whitespace?_ ","_
                Whitespace?_
                Expression{ onFail: fail("Expected function call argument after comma") }
            )*
        )?
        Whitespace?_ ")"_{ onFail: fail("Expected closing parenthesis for function call") }
    )

ExprOpSubscript:
    (
        "["_ Whitespace?_
        Expression{ onFail: fail("Expected subscript index") }
        Whitespace?_ "]"_{ onFail: fail("Expected closing bracket for subscript") }
    )

ExprPrec14:
    "("_ Whitespace?_ Expression Whitespace?_ ")"_{ onFail: fail("Expected closing parenthesis for atom")}
    Identifier
    Literal

\\ ---------------- INDENTATION ----------------

Indentation:
    :IndentationStack.0:

IndentationIncrease:
    ( Indentation Whitespace ){ onMatch: push(_, IndentationStack) }

IndentationDecrease:
    ""_{ onMatch: pop(IndentationStack) }

\\ ---------------- COMMENTS ----------------

Comment:
    Whitespace?_ [ CommentSingleLine CommentMultiLine ]

CommentSingleLine(hidden fuse):
    "\\\\" Newline!*

CommentMultiLine(hidden fuse):
    "\\*" "*\\"!* "*\\"

\\ ---------------- LITERALS ----------------

Literal:
    LiteralInteger
    LiteralString

LiteralInteger(fuse):
    '09'+

LiteralString:
    String

\\ ---------------- STRINGS ----------------

String(fuse):
    (
        "\""_
        StringChar*
        "\""_{ onFail: fail("Expected closing quote for string literal") }
    )

StringChar(hidden):
    EscapeSequence
    "\""!

\\ ---------------- IDENTIFIERS ----------------

Identifier(fuse):
    [ AlphaChar "_" ] [ AlnumChar "_" ]*

\\ ---------------- MISCELLANEOUS ----------------

NonCodeBlock(hidden):
    (
        [ EmptyLine_ Comment ]
        (
            "\n"_
            [ EmptyLine_ Comment ]
        )*
    )
    

EmptyLine:
    Whitespace?_ Newline~_

EscapeSequence(fuse):
    (
        "\\"_
        [
            "a" "b" "e" "f" "n" "r" "t" "v" "\\" "'" "\""
            (
                "x" (HexChar HexChar){ onFail: fail("Expected hexadecimal escape sequence after '\\x'") }
            )
        ]{ onFail: fail("Expected escape sequence after '\\'") }
    )

AlphaChar(hidden): [ 'az' 'AZ' ]
AlnumChar(hidden): [ AlphaChar DecChar ]

BinChar(hidden): '01'
OctChar(hidden): '07'
DecChar(hidden): '09'
HexChar(hidden): [ '09' 'af' 'AF' ]

Newline: "\n"
Whitespace(fuse): [ " " "\t" ]+