# ---------------- GRAMMAR ----------------

Grammar:
	GrammarLine ("\n"_ GrammarLine)*

GrammarLine(hidden):
	RuleDefinition Comment?
	Comment
	Whitespace?_ "\n"~_

# ---------------- RULE DEFINITION ----------------

RuleDefinition:
	RuleHeader (Newline Whitespace)?_ RuleBody

RuleHeader:
	Identifier RuleModifierList?_ ":"_

RuleBody:
	RuleOptionDefinition (Newline Whitespace?_ RuleOptionDefinition)*

# ---------------- RULE MODIFIERS ----------------

RuleModifierList(hidden):
	"("_ RuleModifier (Whitespace_ RuleModifier)* ")"_

RuleModifier:
	"hidden"
	"fuse"

# ---------------- RULE OPTION ----------------

RuleOptionDefinition:
	RuleOption (Whitespace?_ RuleOption)*

RuleOption:
	Matcher RuleOptionModifiers

# ---------------- OPTION MODIFIERS ----------------

RuleOptionModifiers(hidden):
	OptionModifierInvert? OptionModifierQuantifier? OptionModifierLookAhead? OptionModifierOmitMatch? OptionModifierAltName?

OptionModifierInvert:
	"!"_

OptionModifierQuantifier:
	[ "?" "*" "+" ]

OptionModifierLookAhead:
	"~"_

OptionModifierOmitMatch:
	"_"_

OptionModifierAltName:
	"@"_ Identifier

# ---------------- MATCHERS ----------------

Matcher(hidden):
	MatchAnyChar
	MatchAll
	MatchAny
	MatchRange
	MatchExact
	MatchRule

MatchAnyChar:
	"."

MatchAll(fuse):
	"("_ Whitespace?_ RuleOptionDefinition Whitespace?_ ")"_

MatchAny:
	"["_ "^"? Whitespace?_ RuleOptionDefinition Whitespace?_ "]"_

MatchRange:
	"'"_ MatchRangeChar MatchRangeChar "'"_

MatchExact(fuse):
	"\""_ [ "\\\\" "\\\"" "\""! ]+ "\""_

MatchRule:
	Identifier

# ---------------- IDENTIFIER ----------------

Identifier(fuse):
	AlphaChar AlnumChar*

# ---------------- COMMENT ----------------

Comment(fuse):
	"#" "\n"!*

# ---------------- MISCELLANEOUS ----------------

MatchRangeChar:
	"\\"? .

AlphaChar(hidden):
    'az'
    'AZ'

NumChar(hidden):
    '09'

AlnumChar(hidden):
    AlphaChar
    NumChar

Newline(hidden):
    "\n"

Whitespace(fuse):
    [ " " "\t" ]+